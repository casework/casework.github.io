PREFIX case-investigation: <https://ontology.caseontology.org/case/investigation/>
PREFIX uco-action: <https://ontology.unifiedcyberontology.org/uco/action/>
PREFIX uco-core: <https://ontology.unifiedcyberontology.org/uco/core/>
PREFIX uco-identity: <https://ontology.unifiedcyberontology.org/uco/identity/>
PREFIX uco-observable: <https://ontology.unifiedcyberontology.org/uco/observable/>
PREFIX uco-tool: <https://ontology.unifiedcyberontology.org/uco/tool/>

SELECT ?lDeviceExhibitNumber ?lImagingEndTime ?lOfficerName ?lToolName ?lToolVersion ?lImageFileName
WHERE {
  ?nImagingAction
    a case-investigation:InvestigativeAction ;
    uco-action:endTime ?lImagingEndTime ;
    uco-action:instrument ?nImagingTool ;
    uco-action:object ?nSubjectDeviceProvenanceRecord ;
    uco-action:object ?nSubjectDevice ;
    uco-action:performer ?nImagingPerformer ;
    uco-action:result ?nImageFile ;
    .

  ?nSubjectDevice
    a/rdfs:subClassOf* uco-observable:ObservableObject ;
    uco-core:hasFacet ?nSubjectDeviceFacet ;
    .

  ?nSubjectDeviceFacet
    a uco-observable:DeviceFacet ;
    .

  ?nSubjectDeviceProvenanceRecord
    a case-investigation:ProvenanceRecord ;
    case-investigation:exhibitNumber ?lDeviceExhibitNumber ;
    uco-core:object ?nSubjectDevice ;
    .

  ?nImagingPerformer
    a uco-identity:Person ;
    uco-core:hasFacet ?nImagingPerformerIdentityFacet ;
    .

  ?nImagingPerformerIdentityFacet
    a uco-identity:SimpleNameFacet ;
    uco-identity:familyName ?lOfficerName ;
    .

  ?nImagingTool
    a/rdfs:subClassOf* uco-tool:Tool ;
    uco-core:name ?lToolName ;
    uco-tool:version ?lToolVersion ;
    .

  ?nImageFile
    a/rdfs:subClassOf* uco-observable:ObservableObject ;
    uco-core:hasFacet ?nImageFileFacet ;
    .

  ?nImageFileFacet
    a uco-observable:FileFacet ;
    uco-observable:fileName ?lImageFileName ;
    .
}
