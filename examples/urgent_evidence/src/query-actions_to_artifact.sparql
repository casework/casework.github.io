PREFIX case-investigation: <https://ontology.caseontology.org/case/investigation/>
PREFIX uco-action: <https://ontology.unifiedcyberontology.org/uco/action/>
PREFIX uco-core: <https://ontology.unifiedcyberontology.org/uco/core/>
PREFIX uco-observable: <https://ontology.unifiedcyberontology.org/uco/observable/>

SELECT DISTINCT ?nDerivingAction ?lDescription
WHERE {
  # Identify action at end of path.
  ?nEndAction
    uco-action:endTime ?lEndActionEndTime ;
    uco-action:result/uco-core:hasFacet/uco-observable:fileName "IMG_1863.jpg" ;
    .

  # Identify all actions in chain.
  ?nEndAction case-investigation:wasInformedBy* ?nDerivingAction .

  # Extract annotations of actions in chain.
  ?nDerivingAction
    uco-core:description ?lDescription ;
    uco-action:endTime ?lDerivingActionEndTime ;
    .
}
ORDER BY ?lDerivingActionEndTime
