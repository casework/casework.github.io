PREFIX case-investigation: <https://ontology.caseontology.org/case/investigation/>
PREFIX uco-action: <https://ontology.unifiedcyberontology.org/uco/action/>
PREFIX uco-core: <https://ontology.unifiedcyberontology.org/uco/core/>
PREFIX uco-identity: <https://ontology.unifiedcyberontology.org/uco/identity/>

SELECT DISTINCT ?lLastName
WHERE
{
  # Many actions involve the exhibit's device.
  # Look up the exhibit-device based on its exhibit number.
  ?nOriginalProvenanceRecord
    a case-investigation:ProvenanceRecord ;
    case-investigation:exhibitNumber "EXH-20190101-7" ;
    uco-core:object ?nExhibitDevice ;
    .

  # Then, look up the people that produced other provenance records for this device.
  ?nInvestigativeAction
    a case-investigation:InvestigativeAction ;
    uco-action:performer ?nPerformer ;
    uco-action:result ?nActionProvenanceRecord ;
    .

  # This query component ties the provenance records together
  # by object-reference to the device.
  ?nActionProvenanceRecord
    a case-investigation:ProvenanceRecord ;
    uco-core:object ?nExhibitDevice ;
    .

  ?nPerformer
    a uco-identity:Person ;
    uco-core:hasFacet ?nSimpleName ;
    .

  ?nSimpleName
    a uco-identity:SimpleNameFacet ;
    uco-identity:familyName ?lLastName ;
    .
}
ORDER BY ?lLastName
